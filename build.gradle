plugins {
  id 'fabric-loom' version '0.12-SNAPSHOT'
  id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
  maven {
    name = "ModMenu"
    url = "https://maven.terraformersmc.com/releases/"
  }
  maven {
    name = "WorldEdit"
    url = "https://maven.enginehub.org/repo/"
  }
  maven {
    name = 'Roundaround Maven'
    allowInsecureProtocol = true
    url = "https://maven.rnda.dev/releases"
  }
}

dependencies {
  // To change the versions see the gradle.properties file
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  // Test using real account
  modCompileOnly "com.ptsmods:devlogin:2.1"

  // Include WorldEdit in dev environment
  modCompileOnly "com.sk89q.worldedit:worldedit-core:7.2.10"
  modCompileOnly "com.sk89q.worldedit:worldedit-fabric-mc1.19:7.2.11-SNAPSHOT"

  // ModMenu integration
  modCompileOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"

  // RoundaLib for config screen
  modImplementation "me.roundaround:roundalib:${project.roundalib_version}"
  include "me.roundaround:roundalib:${project.roundalib_version}"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 17
}

java {
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archives_base_name}"}
  }
}

tasks.publish.dependsOn build
publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId project.archives_base_name
      version = project.mod_version + "+" + project.minecraft_version

      artifact(remapJar) {
        builtBy remapJar
      }
    }
  }
  repositories {
    maven {
      allowInsecureProtocol = true
      url = "https://maven.rnda.dev/releases"
      credentials {
        username = selfHostedMavenUser
        password = selfHostedMavenPass
      }
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
}
